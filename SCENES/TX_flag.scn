//---------------------------------------------------------
//	The Texas State Flag
//---------------------------------------------------------
/*
 The state flag consists of a rectangle with a width to length ratio of
 two to three containing:
 (1) a blue vertical stripe one-third the entire length of the flag wide,
 and two equal horizontal stripes, the upper stripe white, the lower red,
 each two-thirds the entire length of the flag long;
 and (2) a white, regular five-pointed star in the center of the blue stripe,
 oriented so that one point faces upward, and of such a size that the diameter
 of a circle passing through the five points of the star is equal to
 three-fourths the width of the blue stripe."
*/

// Include a collection of pre-defined colors.
include "colors.inc"

// Include some pre-defined basic surface types.
include "basicsurfs.inc"

define TexasFlag object
{
	polygon
	{
		vertex <0.0, 0.0, 0.0>;
		vertex <0.333, 0.0, 0.0>;
		vertex <0.333, -0.667, 0.0>;
		vertex <0.0, -0.667, 0.0>;

		Matte { color = Blue; }
	}
	polygon
	{
		vertex <0.333, 0.0, 0.0>;
		vertex <1.0, 0.0, 0.0>;
		vertex <1.0, -0.333, 0.0>;
		vertex <0.333, -0.333, 0.0>;

		Matte { color = White * 2; }
	}
	polygon
	{
		vertex <0.333, -0.333, 0.0>;
		vertex <1.0, -0.333, 0.0>;
		vertex <1.0, -0.667, 0.0>;
		vertex <0.333, -0.667, 0.0>;

		Matte { color = Red; }
	}

	polygon
	{
		float th = 0;
		repeat (5)
		{
			vertex <sin(th), cos(th), 0.0> * 0.125 + <0.167, -0.333, 0.001>;
			th = th + 0.628318;
			vertex <sin(th), cos(th), 0.0> * 0.125 * 0.38 + <0.167, -0.333, 0.001>;
			th = th + 0.628318;
		}

		Matte { color = White * 2; }
	}
}

// Main entry point to generate the scene
main
{
	viewport
	{
		from = <0, -1, 10>;
		at = <0, 0, 0>;
		angle = 30;
	}

	background White, SkyBlue
	{
	}

	light
	{
		location = <25, -100, 100>;
		color = 0.5;
	//	falloff = 0.0001;
	}
	light
	{
		location = <-100, -50, 100>;
		color = 0.5;
	//	falloff = 0.0001;
	}

	TexasFlag
	{
		translate <-0.5, 0.333, 0>;
		scale 5;
	}
}
